#compdef kss
local ret=1 state
local -a namespace pods
local kubectl=kubectl

args=(
    {-h,--help}'[display help message]' \
    {-l,--showlog}'[Show log]' \
    {-r,--restrict}'[Retrict pods to]: :' \
    {-n,--namespace}'[Use namespace]:Use namespace:->namespace' \
    '*:pods:->pods'
)

_arguments -S -C $args && ret=0

case $state in
  namespace)
      namespace=(${(@f)$(_call_program pod ${kubectl} get namespace -o name)#namespace/##})
      _describe 'all namespace' namespace && ret=0
      ;;
  pods)
      for (( i = 1; i <= $#words - 1; i++ )); do
          if [[ $words[$i] == -n ]]; then
              kubectl="kubectl -n $words[$((i+1))]"
              break
          fi
      done
      pods=(${(@f)$(_call_program pod ${kubectl} get pod -o name)#pod/##})
      _describe 'all pods' pods && ret=0
      ;;
esac

return ret


# Local Variables:
# mode: shell-script
# End:
