#compdef kss
local ret=1 state
local -a namespace pods
local kubectl=kubectl

(( $+functions[_kss_cache_policy] )) ||
_kss_cache_policy () {
  typeset -a old

  # cache is valid for 1mn
  old=( "$1"(mm+1) )
  (( $#old ))
}

local args=(
    {-h,--help}'[display help message]' \
    {-l,--showlog}'[Show log]' \
    {-r,--restrict}'[Retrict pods to]: :' \
    {-n,--namespace}'[Use namespace]:Use namespace:->namespace' \
    '*:pods:->pods'
)

_arguments -S -C $args && ret=0

case $state in
  namespace)
      zstyle ":completion:${curcontext}:" cache-policy _kss_cache_policy
      local cacheid=kubectl_namespaces
      typeset -a namespaces
      local namespaceflg
      if _cache_invalid "$cacheid" || ! _retrieve_cache "$cacheid"
      then
          namespaces=(${(@f)$(command ${kubectl} get namespace -o name)#namespace/##})
          _store_cache "$cacheid" namespaces
      fi
      _describe 'all namespace' namespaces && ret=0
      ;;
  pods)
      for (( i = 1; i <= $#words - 1; i++ )); do
          if [[ $words[$i] == -n || $words[$i] == --namespace  ]]; then
              kubectl="kubectl --namespace $words[$((i+1))]"
              break
          fi
      done
      pods=(${(@f)$(_call_program pod ${kubectl} get pod -o name)#pod/##})
      _describe 'all pods' pods && ret=0
      ;;
esac

return ret


# Local Variables:
# mode: shell-script
# End:
